// Taken from https://www.cl.cam.ac.uk/~jac22/books/www/book/node166.html
<fragmentaddress> ::= <uri> | <uri> "#" <fragmentid>
<uri> ::= <url>
<url> ::= <generic> | <httpaddress> | <ftpaddress> | <newsaddress> | <prosperoaddress> | <telnetaddress> | <gopheraddress> | <waisaddress>
<generic> ::= <scheme> ":" <path> | <scheme> ":" <path> "?" <search>
<scheme> ::= <ialpha>
<httpaddress> ::= "http://" <hostport> | "http://" <hostport> "/" <path> | "http://" <hostport> "/" <path> "?" <search>
<ftpaddress> ::= "ftp://" <login> "/" <path>
<afsaddress> ::= "afs://" <cellname> "/" <path>
<newsaddress> ::= "news:" <groupart>
<waisaddress> ::= <waisindex> | <waisdoc>
<waisindex> ::= "wais://" <hostport> "/" <database> | "wais://" <hostport> "/" <database> "?" <search>
<waisdoc> ::= "wais://" <hostport> "/" <database> "/" <wtype> "/" <digits> "/" <path>
<groupart> ::= "*" | <group> | <article>
<group> ::= <ialpha> | <ialpha> "." <group>
<article> ::= <xalphas> "@" <host>
<database> ::= <xalphas>
<wtype> ::= <xalphas>
<prosperoaddress> ::= <prosperolink>
<prosperolink> ::= "prospero://" <hostport> "/" <hsoname> | "prospero://" <hostport> "/" <hsoname> "%00" <version> | "prospero://" <hostport> "/" <hsoname> "%00" <version> <attributes>
<hsoname> ::= <path>
<version> ::= <digits>
<attributes> ::= <attribute> | <attribute> <attributes>
<attribute> ::= <alphanums>
<telnetaddress> ::= "telnet://" <login>
<gopheraddress> ::= "gopher://" <hostport> | "gopher://" <hostport> "/" <gtype> | "gopher://" <hostport> "/" <gtype> <selector> | "gopher://" <hostport> "/" <gtype> <selector> "?" <search>
<login> ::= <hostport> | <user> "@" <hostport> | <user> ":" <password> "@" <hostport>
<hostport> ::= <host> | <host> ":" <port>
<host> ::= <hostname> | <hostnumber>
<cellname> ::= <hostname>
<hostname> ::= <ialpha> | <ialpha> "." <hostname>
<hostnumber> ::= <digits> "." <digits> "." <digits> "." <digits>
<port> ::= <digits>
<selector> ::= <path>
<path> ::= "" | <xpalphas> | <xpalphas> "/" <path>
<search> ::= <xalphas> | <xalphas> "+" <search>
<user> ::= <xalphas>
<password> ::= <xalphas>
<fragmentid> ::= <xalphas>
<gtype> ::= <xalpha>
<xalpha> ::= <alpha> | <digit> | <safe> | <extra> | <escape>
<xalphas> ::= <xalpha> | <xalpha> <xalphas>
<xpalpha> ::= <xalpha> | "+"
<xpalphas> ::= <xpalpha> | <xpalpha> <xpalphas>
<ialpha> ::= <alpha> | <alpha> <xalphas>
<alpha> ::= "a" | "b" | "c" | "d" | "e" | "f" | "g" | "h" | "i" | "j" | "k" | "l" | "m" | "n" | "o" | "p" | "q" | "r" | "s" | "t" | "u" | "v" | "w" | "x" | "y" | "z" | "A" | "B" | "C" | "D" | "E" | "F" | "G" | "H" | "I" | "J" | "K" | "L" | "M" | "N" | "O" | "P" | "Q" | "R" | "S" | "T" | "U" | "V" | "W" | "X" | "Y" | "Z"
<digit> ::= "0" | "1" | "2" | "3" | "4" | "5" | "6" | "7" | "8" | "9"
<safe> ::= "$" | "-" | "_" | "@" | "." | "&"
<extra> ::= "!" | "*" | "\"" | "'" | "(" | ")" | ":" | ";" | "," | " "
<escape> ::= "%" <hex> <hex>
<hex> ::= <digit> | "a" | "b" | "c" | "d" | "e" | "f" | "A" | "B" | "C" | "D" | "E" | "F"
<national> ::= "{" | "}" | "|" | "[" | "]" | "\\" | "^" | "~"
<punctuation> ::= "<" | ">"
<digits> ::= <digit> | <digit> <digits>
<alphanum> ::= <alpha> | <digit>
<alphanums> ::= <alphanum> | <alphanum> <alphanums>